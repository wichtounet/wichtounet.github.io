For <a title="EDDI Compiler 1.1.1 – Dynamic Memory Allocation and Constructors/Destructors" href="http://www.baptiste-wicht.com/2012/07/eddi-compiler-1-1-1-dynamic-memory-allocation-constructors-destructors/">the last version of the EDDI Compiler</a>, it has been necessary to extend the dynamic memory allocator, to support free memory. In this post, we will see how to write a simple Memory Manager in Intel Assembly for Linux.

In the past, we've seen <a title="Dynamic memory allocation in Intel Assembly on Linux" href="http://www.baptiste-wicht.com/2011/11/dynamic-memory-allocation-intel-assembly-linux/">how to write a basic memory allocator</a>, this time, we will write a more complete version.

The implementation is made in 64bits Intel Assembly.

<h3>Memory Manager specification</h3>

The memory will be allocated by blocks. Each block will contain a header with two information:

<ul>
	<li>A boolean flag indicating if the block is free or not</li>
	<li>The size of the block (including the header)</li>
</ul>

Each time some memory is asked, the blocks are tested one by one until an available one is found. If no available block is found, a new block is allocated after the last one and this block is returned.

The memory manager consists of three functions:

<ul>
	<li>memory_init: Init the memory manager</li>
	<li>memory_alloc: Allocate the given number of bytes of memory</li>
	<li>memory_free: Release the given block</li>
</ul>

The parameter is passed in the <strong>r14</strong> register. The return value is returned in the <strong>rax</strong> register.

<h3>Global State</h3>

This implementation needs two global variables. One for the start address of memory and the other one for the last:

```nasm
section .data
mem_last dq 0
mem_start dq 0
```

<h3>Init memory Manager</h3>

The init function is very simple to implement:

```nasm
init:
push rbp
mov rbp, rsp
mov rax, 12
xor rdi, rdi
syscall
mov [mem_start], rax
mov [mem_last], rax
leave
ret
```

We just have to call sys_brk in order to get the location of <em>program break</em>. Then, the start and the last addresses are the same.

<h3>Free memory</h3>

The free function is the simplest one:

```nasm
free:
push rbp
mov rbp, rsp
mov qword [r14 - 16], 1
leave
ret
```

The address to free is passed in the <strong>r14</strong> register. We have to go back 16 bytes (size of the control block) to go to the start of the block. The availability flag is set to 1 (the block is free).

<h3>The alloc function</h3>

The alloc function is the most complex:

```nasm
alloc:
push rbp
mov rbp, rsp
push rdi
push r10
push r11
push r12
push r13
push r14
add r14, 16
mov r12, [mem_start]
mov r13, [mem_last]
.start:
cmp r12, r13
je .alloc
mov r10, [r12]
mov r11, [r12 + 8]
cmp r10, 1
jne .move
cmp r11, r14
jl .move
mov qword [r12], 0
lea rax, [r12 + 16]
pop r14
pop r13
pop r12
pop r11
pop r10
pop rdi
leave
ret

.move:
add r12, r11
jmp .start

.alloc:
lea rdi, [r12 + r14]
mov rax, 12
syscall
mov [mem_last], rdi
mov qword [r12], 0
mov qword [r12 + 8], r14
lea rax, [r12 + 16]
pop r14
pop r13
pop r12
pop r11
pop r10
pop rdi
leave
ret
```

As the function is a bit complex, I will detail it in part:

```nasm
add r14, 16
mov r12, [mem_start]
mov r13, [mem_last]
.start:
cmp r12, r13
je .alloc
mov r10, [r12]
mov r11, [r12 + 8]
cmp r10, 1
jne .move
cmp r11, r14
jl .move
mov qword [r12], 0
lea rax, [r12 + 16]
```

The necessary number of bytes is passed in the <strong>r14</strong> register. We add 16 bytes (size of the control group) to the size as we also need some place for the header. Then, we load the start and last addresses. If both addresses are equal, we need to allocate more memory (detailed later). Then, we check the size and the availability of the current block. If the size is enough to fit the needs and the block is available, we set it to unavailable. We return the address past the control block (16 bytes).

```nasm
.move:
add r12, r11
jmp .start
```

To move to the next block, we just have to add the size of the current block to the current block address.

```nasm
.alloc:
lea rdi, [r12 + r14]
mov rax, 12
syscall
mov [V_mem_last], rdi
mov qword [r12], 0
mov qword [r12 + 8], r14
lea rax, [r12 + 16]
```

To allocate memory, we compute the new <em>program break</em> and call <em>sys_brk</em> again to set the new <em>program break</em>. The block is then set to not available and the size is set. We return the address past the control block (16 bytes).

The rest of the program is just here to save and restore the registers and compute the stack frames.

<h3>Wrap-Up</h3>

In this article, we saw how to implement a very simple memory manager in 64bits Intel Assembly on Linux. This memory manager is very simple, but has several drawbacks:

<ul>
	<li>The overhead for small blocks is important. For example, allocating an 8 bytes integer needs a 24 bytes block, thrice the size of the int.</li>
	<li>In the worst-case scenario, all of the process memory need to be walked across to find a new free block</li>
	<li>The functions are not thread-safe</li>
	<li>This algorithm can lead to a lot of memory fragmentation</li>
</ul>

In the future I will try to make a more powerful version of this memory manager.

<h3>Download</h3>

All the functions are available online on the Github Repository:

<ul>
	<li><a href="https://github.com/wichtounet/eddic/blob/develop/functions/x86_64_alloc.s">alloc</a></li>
	<li><a href="https://github.com/wichtounet/eddic/blob/develop/functions/x86_64_free.s">free</a></li>
	<li><a href="https://github.com/wichtounet/eddic/blob/develop/functions/x86_64_init.s">init</a></li>
</ul>

They are also available in 32bits Intel Assembly:

<ul>
	<li><a href="https://github.com/wichtounet/eddic/blob/develop/functions/x86_32_alloc.s">alloc</a></li>
	<li><a href="https://github.com/wichtounet/eddic/blob/develop/functions/x86_32_free.s">free</a></li>
	<li><a href="https://github.com/wichtounet/eddic/blob/develop/functions/x86_32_init.s">init</a></li>
</ul>